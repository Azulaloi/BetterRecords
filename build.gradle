buildscript {
    repositories {
        mavenCentral()
        mavenLocal()
        maven {
            name = 'ForgeFS'
            url = 'http://files.minecraftforge.net/maven'
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT'
    }
}

repositories {
    flatDir {
        dirs 'lib'
    }
}

apply plugin: 'forge'

version = "$minecraft_version-$mod_version"
group = "com.codingforcookies.betterrecords"
archivesBaseName = "betterrecords"

sourceCompatibility = 1.7
targetCompatibility = 1.7

minecraft {
    version = "$minecraft_version-$forge_version"
    runDir = 'run'
    replace '@VERSION@', project.version
}

dependencies {
    compile name: 'mp3plugin'
    compile name: 'mp3spi1.9.5'
    compile name: 'tritonus-share-0.3.7-2'
    compile name: 'vorbisspi-1.0.3-1'
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

task deobfJar(type: Jar) {
    from sourceSets.main.output
    classifier = 'deobf'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    classifier = 'sources'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives deobfJar
    archives sourcesJar
    archives javadocJar
}
